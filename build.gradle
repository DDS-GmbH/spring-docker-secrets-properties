plugins {
	id 'org.springframework.boot' version '2.6.2'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'maven-publish'
}

group = 'com.docutools'
version = '1.1.1'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
}

publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			url "https://maven.pkg.github.com/dds-gmbh/spring-docker-secrets-properties"
			credentials {
				username = project.findProperty("GITHUB_USERNAME") ?: System.getenv("GITHUB_USERNAME")
				password = project.findProperty("GITHUB_TOKEN") ?: System.getenv("GITHUB_TOKEN")
			}
		}
	}
	publications {
		gpr(MavenPublication) {
			from(components.java)
			artifactId = 'spring-docker-secrets-properties'
			versionMapping {
				usage('java-api') {
					fromResolutionOf('runtimeClasspath')
				}
				usage('java-runtime') {
					fromResolutionResult()
				}
			}
		}
	}
}


java {
	withSourcesJar()
	withJavadocJar()
}

bootJar {
	enabled = false
}

jar {
	enabled = true
}